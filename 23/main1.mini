let Input := file(Args[1], "r")
let Lines := list(seq Input:read{it != "\n"}:trim)
print('Lines = {Lines}\n')

let H := Lines:length
let W := Lines[1]:length

let Grid := array::int8([H, W])

for R, Line in Lines do
	for C, X in Line do
		Grid[R, C] := "#.^>v<":find(X)
	end
end

for Y in 1 .. H do print(Grid[Y], "\n") end
print("\n")

class: path(:points, :oneway)

let Paths := []
let Start := (1, first(1:up ->? fun(C) Grid[1, C] = 2))
let Queue := [(nil, Start)]
let Done := set([Start])

loop
	print(Queue, "\n")
	var (From, Point) := while Queue:pop
	let Points := []
	if From then Points:put(From) end
	loop
		Points:put(Point)
		if Point[1] = H then
			Paths:put(path(Points))
			exit
		end
		let (R, C) := Point, Next := set()
		Grid[R - 1, C] > 1 and Next:insert((R - 1, C))
		Grid[R + 1, C] > 1 and Next:insert((R + 1, C))
		Grid[R, C - 1] > 1 and Next:insert((R, C - 1))
		Grid[R, C + 1] > 1 and Next:insert((R, C + 1))
		Next:delete(Points[-2]{})
		if Next:size > 1 then
			Paths:put(path(Points))
			if Done:missing(Point) then
				for To in Next do
					Queue:put((Point, To))
				end
			end
			exit
		end
		Point := while first(Next)
	end
end

let Waypoints := {}

for Path in Paths do
	var Direction := 0
	var (R1, C1) := Path:points[1]
	for (R2, C2) in Path:points skip 1 do
		switch Grid[R1, C1]: integer
		case 3 do
			if R2 < R1 then
				Direction := old \/ 1
			elseif R2 > R1 then
				Direction := old \/ 2
			else
				Direction := 3
			end
		case 4 do
			if C2 > C1 then
				Direction := old \/ 1
			elseif C2 < C1 then
				Direction := old \/ 2
			else
				Direction := 3
			end
		case 5 do
			if R2 > R1 then
				Direction := old \/ 1
			elseif R2 < R1 then
				Direction := old \/ 2
			else
				Direction := 3
			end
		case 6 do
			if C2 < C1 then
				Direction := old \/ 1
			elseif C2 > C1 then
				Direction := old \/ 2
			else
				Direction := 3
			end
		end
		R1 := R2
		C1 := C2
	end
	let Start := Path:points[1]
	let End := Path:points[-1]
	let Length := Path:points:length - 1
	switch Direction: integer
	case 0 do
		Waypoints[Start, /list]:put((End, Length))
		Waypoints[End, /list]:put((Start, Length))
	case 1 do
		Waypoints[Start, /list]:put((End, Length))
	case 2 do
		Path:points:reverse
		Path:oneway := 1
	case 3 do
	end
	:>print(Path, " -> ", Direction, "\n\n")
end

let Graph := file("graph.dot", "w")
let Names := {}
Graph:write("digraph {\n")
for Point, Next in Waypoints do
	for (Point2, Length) in Next do
		Graph:write('\tP_{Point[1]}_{Point[2]} -> P_{Point2[1]}_{Point2[2]} [label="{Length}"];\n')
	end
end
Graph:write("}")
Graph:close

Queue:empty
Queue:put((Start, 0))
var Max := 0
loop
	let (Point, Length, Done) := while Queue:pop
	if Point[1] = H then
		Max := old max Length
	end
	for Next in Waypoints[Point] do
		Queue:put((Next[1], Length + Next[2]))
	end
end
print(Max)
